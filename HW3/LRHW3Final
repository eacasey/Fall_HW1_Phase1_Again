/* Created new variables for each variable with missing values that replace missing values with new missing categories */
/* Do transformation on CASHBK AND MMCRED now to roll up the 2nd level into the first for Number of Cash Back */
/* and the 5th rolevel of Money Market Credits  */

data lrhw3.t_transform;
	set LRHW3.insurance_t_bin;
	if cc = . then cc_2=2; *2 = missing category;
	else cc_2=cc;
	if ccpurc = . then ccpurc_2=5; *5 is missing category;
	else ccpurc_2=ccpurc;
	if hmown="." then hmown_2 =2; *2 is missing category;
	else hmown_2=hmown;
	if inv="." then inv_2=2; *2 is missing category;
	else inv_2=inv;
	if cashbk = 2 then cashbk = 1; /* this adjusts for the only 2 obs that had a 2 for cashbk and were both 0's for ins */
	if mmcred = 5 then mmcred = 3; /* this adjusts for only 1 obs w/ a mmcred = 5 and rolls up to 3 since 0 obs = 4*/
run;

/* CREATE MACRO VARIABLE OF ALL VARIABLES THAT ARE BEING CONSIDERED TO GO INTO THE MODEL */
%LET MAINEFFECTS = DDA CASHBK DIRDEP NSF SAV ATM CD IRA LOC INV_2 ILS
MM MMCRED MTG CC_2 CCPURC_2 SDB HMOWN_2 MOVED INAREA BRANCH RES DDABAL_BIN
ACCTAGE_BIN DEPAMT_BIN CHECKS_BIN NSFAMT_BIN PHONE_BIN TELLER_BIN
SAVBAL_BIN ATMAMT_BIN POS_BIN POSAMT_BIN CDBAL_BIN IRABAL_BIN LOCBAL_BIN
INVBAL_BIN ILSBAL_BIN MMBAL_BIN MTGBAL_BIN CCBAL_BIN INCOME_BIN LORES_BIN
HMVAL_BIN AGE_BIN CRSCORE_BIN;

/* CREATE MACRO VARIABLE FOR VARIABLES THAT WILL GO INTO THE CLASS STATEMENT */
%LET main_ordinal =  MMCRED CCPURC_2(PARAM=REF REF='0') DDABAL_BIN(PARAM=REF REF='1') ACCTAGE_BIN DEPAMT_BIN CHECKS_BIN NSFAMT_BIN PHONE_BIN TELLER_BIN
		     SAVBAL_BIN ATMAMT_BIN POS_BIN POSAMT_BIN CDBAL_BIN IRABAL_BIN LOCBAL_BIN INVBAL_BIN
 		     ILSBAL_BIN MMBAL_BIN MTGBAL_BIN CCBAL_BIN INCOME_BIN LORES_BIN HMVAL_BIN AGE_BIN CRSCORE_BIN;

/* Concordance Table */
proc logistic data=LRHW3.t_transform plots(only)=(oddsratio);
	CLASS &main_ordinal BRANCH RES  CC_2(PARAM=REF REF='0') 
		   HMOWN_2(PARAM=REF REF='0') INV_2(PARAM=REF REF='0');
	model INS(event='1') = &MAINEFFECTS / clodds=pl clparm=pl;
	title 'Modeling Insurance Project';
run;


/* Discrimination Slope */
proc logistic data=LRHW3.t_transform noprint;
	CLASS &main_ordinal BRANCH RES  CC_2(PARAM=REF REF='0') 
		   HMOWN_2(PARAM=REF REF='0') INV_2(PARAM=REF REF='0');
	model INS(event='1') = &MAINEFFECTS;
	output out=predprobs p=phat; 
run;

proc sort data=predprobs;
	by descending ins;
run;

proc ttest data=predprobs order=data;
	ods select statistics summarypanel;
	class INS;
	var phat;
	title 'Coefficient of Discrimination and Plots';
run;

/* Running regression */

proc logistic data=LRHW3.t_transform noprint;
	CLASS &main_ordinal BRANCH RES  CC_2(PARAM=REF REF='0') 
		   HMOWN_2(PARAM=REF REF='0') INV_2(PARAM=REF REF='0');
	model INS(event='1') = &MAINEFFECTS;
	output out = predprobs p = phat;
run;

proc npar1way data = predprobs d plot = edfplot;
	CLASS INS; *dependent variable here;
	var phat;
run;

/* k-s statistic = 0.4778 */
/* Value of phat at Maximum = 0.288265 */
/*compare ROC curves*/
proc logistic data=LRHW3.T_TRANSFORM;
	class 
	DDA 
	NSF 
	IRA
	inv_2 (param=ref ref='0')
	ILS 
	MM 
	cc_2 (param=ref ref='0')
	BRANCH (param=effect ref='B9')
	DDABAL_Bin (param=ref ref='1')
	CHECKS_Bin (param=effect ref='4')
	TELLER_Bin (param=effect ref='3')
	SAVBAL_Bin (param=effect ref='7')
	ATMAMT_Bin (param=effect ref='3')
	CDBAL_Bin (param=effect ref='3');
	model INS(event = '1') = DDA NSF IRA inv_2 ILS MM cc_2 BRANCH DDABAL_Bin
	CHECKS_Bin TELLER_Bin SAVBAL_Bin ATMAMT_Bin CDBAL_Bin / clodds=pl clparm=pl;
	ROC 'omit DDA' NSF IRA inv_2 ILS MM cc_2 BRANCH DDABAL_Bin
	CHECKS_Bin TELLER_Bin SAVBAL_Bin ATMAMT_Bin CDBAL_Bin;
	* add other curves;
	roccontrast / estimate = allpairs;
run;

/*create a confusion matrix with different thresholds*/

data lrhw3.v_transform;
	set lrhw2.insurance_v_bin;
	if cc = . then cc_2=2; *2 = missing category;
	else cc_2=cc;
	if ccpurc = . then ccpurc_2=5; *5 is missing category;
	else ccpurc_2=ccpurc;
	if hmown="." then hmown_2 =2; *2 is missing category;
	else hmown_2=hmown;
	if inv="." then inv_2=2; *2 is missing category;
	else inv_2=inv;
	if cashbk = 2 then cashbk = 1; /* this adjusts for the only 2 obs that had a 2 for cashbk and were both 0's for ins */
	if mmcred = 5 then mmcred = 3; /* this adjusts for only 1 obs w/ a mmcred = 5 and rolls up to 3 since 0 obs = 4*/
run;

proc logistic inmodel=lrhw3.our_training_model;
	score data=lrhw3.v_transform out=lrhw3.valid_fit;
run;

data lrhw3.compare_cutoffs;
	set lrhw3.valid_fit;
	keep Actual Predicted P_0 P_1 Youden_cutoff F1_cutoff KS_cutoff;
	Actual = F_INS;
	Predicted = I_INS;
	Youden_cutoff = 0.290;
	F1_cutoff = 0.290;
	KS_cutoff = 0.296560;
run;

data lrhw3.youden_F1_matrix;
	set lrhw3.compare_cutoffs;
	keep TP TN FP FN;
	cutoff = Youden_cutoff;
	TP = 0;
	TN = 0;
	FP = 0;
	FN = 0;
	if P_1 > cutoff and Actual = 1 then TP = 1;
	else if P_1 > cutoff and Actual = 0 then FP = 1;
	else if P_1 < cutoff and Actual = 0 then TN = 1;
	else if P_1 < cutoff and Actual = 1 then FN = 1;
run;

data lrhw3.KS_matrix;
	set lrhw3.compare_cutoffs;
	keep TP TN FP FN;
	cutoff = KS_cutoff;
	TP = 0;
	TN = 0;
	FP = 0;
	FN = 0;
	if P_1 > cutoff and Actual = 1 then TP = 1;
	else if P_1 > cutoff and Actual = 0 then FP = 1;
	else if P_1 < cutoff and Actual = 0 then TN = 1;
	else if P_1 < cutoff and Actual = 1 then FN = 1;
run;
/*Export data*/
proc export data=lrhw3.youden_F1_matrix
outfile = '/home/u42027047/Fall_LogisticRegression/HW/HW3/outfiles/lrhw3.youden_F1_matrix.csv'
dbms=csv replace;
run;
/*Export data*/
proc export data=lrhw3.KS_matrix
outfile = '/home/u42027047/Fall_LogisticRegression/HW/HW3/outfiles/lrhw3.KS_matrix.csv'
dbms=csv replace;
run;	
/*comparing cutoff values*/
data lrhw3.matrix_05;
	set lrhw3.compare_cutoffs;
	keep TP TN FP FN;
	cutoff = 0.5;
	TP = 0;
	TN = 0;
	FP = 0;
	FN = 0;
	if P_1 > cutoff and Actual = 1 then TP = 1;
	else if P_1 > cutoff and Actual = 0 then FP = 1;
	else if P_1 < cutoff and Actual = 0 then TN = 1;
	else if P_1 < cutoff and Actual = 1 then FN = 1;
run;
proc export data=lrhw3.matrix_05
outfile = '/home/u42027047/Fall_LogisticRegression/HW/HW3/outfiles/lrhw3.matrix_05.csv'
dbms=csv replace;
run;
/*comparing cutoff values*/
data lrhw3.matrix_08;
	set lrhw3.compare_cutoffs;
	keep TP TN FP FN;
	cutoff = 0.8;
	TP = 0;
	TN = 0;
	FP = 0;
	FN = 0;
	if P_1 > cutoff and Actual = 1 then TP = 1;
	else if P_1 > cutoff and Actual = 0 then FP = 1;
	else if P_1 < cutoff and Actual = 0 then TN = 1;
	else if P_1 < cutoff and Actual = 1 then FN = 1;
run;
data lrhw1.v_transform;
	set lrhw1.insurance_v_bin;
	if cc = . then cc_2=2; *2 = missing category;
	else cc_2=cc;
	if ccpurc = . then ccpurc_2=5; *5 is missing category;
	else ccpurc_2=ccpurc;
	if hmown="." then hmown_2 =2; *2 is missing category;
	else hmown_2=hmown;
	if inv="." then inv_2=2; *2 is missing category;
	else inv_2=inv;
	if cashbk = 2 then cashbk = 1; /* this adjusts for the only 2 obs that had a 2 for cashbk and were both 0's for ins */
	if mmcred = 5 then mmcred = 3; /* this adjusts for only 1 obs w/ a mmcred = 5 and rolls up to 3 since 0 obs = 4*/
run;

/* CREATE MACRO VARIABLE OF ALL VARIABLES THAT ARE BEING CONSIDERED TO GO INTO THE MODEL */
%LET MAINEFFECTS = DDA CASHBK DIRDEP NSF SAV ATM CD IRA LOC INV_2 ILS
MM MMCRED MTG CC_2 CCPURC_2 SDB HMOWN_2 MOVED INAREA BRANCH RES DDABAL_BIN
ACCTAGE_BIN DEPAMT_BIN CHECKS_BIN NSFAMT_BIN PHONE_BIN TELLER_BIN
SAVBAL_BIN ATMAMT_BIN POS_BIN POSAMT_BIN CDBAL_BIN IRABAL_BIN LOCBAL_BIN
INVBAL_BIN ILSBAL_BIN MMBAL_BIN MTGBAL_BIN CCBAL_BIN INCOME_BIN LORES_BIN
HMVAL_BIN AGE_BIN CRSCORE_BIN;

/* CREATE MACRO VARIABLE FOR VARIABLES THAT WILL GO INTO THE CLASS STATEMENT */
%LET main_ordinal =  MMCRED CCPURC_2(PARAM=REF REF='0') DDABAL_BIN(PARAM=REF REF='1') ACCTAGE_BIN DEPAMT_BIN CHECKS_BIN NSFAMT_BIN PHONE_BIN TELLER_BIN
		     SAVBAL_BIN ATMAMT_BIN POS_BIN POSAMT_BIN CDBAL_BIN IRABAL_BIN LOCBAL_BIN INVBAL_BIN
 		     ILSBAL_BIN MMBAL_BIN MTGBAL_BIN CCBAL_BIN INCOME_BIN LORES_BIN HMVAL_BIN AGE_BIN CRSCORE_BIN;
/*ROC curve*/
/* Trying to access previous model here by recreating through same code as before but w outmodel statement for later use */
proc logistic data=LRHW1.t_transform outmodel=train_model plots(only)=(oddsratio ROC);
	CLASS  &main_ordinal BRANCH RES  CC_2(PARAM=REF REF='0') 
		   HMOWN_2(PARAM=REF REF='0') INV_2(PARAM=REF REF='0') ;
	model INS(event='1') = &MAINEFFECTS / 
		selection=backward slstay=.002
		clodds=pl clparm=pl ctable pprob=0 to 0.98 by 0.01;
	ods output classification= classtable;
	title 'Modeling Purchase of Insurance Product';
run;
/* the variables cc_2 and level b8 have no p val in the analysis of max likelihood effects, does this matter? */

/* Check Youden Index */
data youden;
	set classtable;
	youden = sensitivity + specificity - 100;
	drop ppv npv correct;
run;

proc sort data=youden;
	by descending youden;
run;
/* Youden's index found that .29 was best cutoff */
/* Think F1 score is a better metric for this problem though 
if going to use a balanced metric so check that too */
data F1;
	set classtable;
	F1 = 2*(PPV*Sensitivity)/(PPV + Sensitivity);
	drop specificity npv correct;
run;

proc sort data=f1;
	by descending F1;
run;

/* well bad news for me is that f1 agrees w Youden that .29 is best cut off so for now just going to */
/* Create a scored dataset to use to create the validation's confusion matrix */

/* Scoring function */
proc logistic inmodel=work.train_model;
	score data=lrhw1.v_transform out=score outroc=roc;
run;

/* using scored dataset create prediction and actual columns for easy confusion matrix creation */
data youden_cutoff;
	set score;
	actual = f_ins;
	if p_1 > 0.29 then predicted = 1;
	else predicted =0;
run;

/* Create confusion matrix for youden/f1 value*/
proc freq data=new_cutoff;
	tables actual*predicted;
run;
/* Current Model Stats on valid set with a .29 cutoff */
/* accuracy = 69.11% */
/* Sensitivity/Recall(TPR) = 77.63% */
/* 1 - specificity(FPR) = 35.46% */
/* Precision (PPV) = 54.03% */

/* Really not sure this is the best model can do both for general accuracy andso going to check both lift curves and create an estimated cost */
/* where False Positives are double the cost of a false negative so cost = 2(FP) + 1(FN) 
this would only really apply in the case of a salesperson being more heavily involved with selling to the model's recommendations
but this provides a good contrasting example of merits of maybe using both cut offs but for different marketing activities*/
data cost;
	set classtable;
	cost = (2*IncorrectEvents) + IncorrectNonevents;
run;

proc sort data= cost;
	by cost;
run;
/* This method says that a cut off of .76 minimizes the estimated cost variable */

data strict_cutoff;
	set score;
	actual = f_ins;
	if p_1 > 0.76 then predicted = 1;
	else predicted = 0;
run;

proc freq data = strict_cutoff;
	tables actual*predicted;
run;
/* Current Model Stats on valid set with a .76 cutoff */
/* accuracy = 67.89% */
/* Sensitivity/Recall(TPR) = 14.92% */
/* 1 - specificity(FPR) = 3.69% */
/* Precision (PPV) = 68.52% */

/* dropping cut off vals down to 0.6 results in a lot more positive results for not much extra cost in how often predicted values are 1's */
/* notice the TPR OF 35.71 compared to only about 15% for a .76 cutoff so depending on how costly the marketing either of these cutoffs coudl make sense */
/* for a higher threshold "priority" or followup type list that is used by the salespeople to prioritize their prospecting time */
/* Current Model Stats on valid set with a .6 cutoff */
/* accuracy = 71.52% */
/* Sensitivity/Recall(TPR) = 35.71% */
/* 1 - specificity(FPR) = 9.26% */
/* Precision (PPV) = 67.43% */

/* Work on lift now */

/* First need to find true pop proportion of 1's in total dataset */
proc freq data=lrhw1.insurance_v_bin;
	tables ins;
run;
/* 742 yes and 2124 total */

proc freq data=lrhw1.insurance_t_bin;
	tables ins;
run;
/* 2918 yes 8495 total */
/* total pop prop of 1's is equal to: 0.34467 */

/* Create lift chart */
data roc; 
	set roc; 
	cutoff = _PROB_; 
	specif = 1-_1MSPEC_; 
	depth=(_POS_+_FALPOS_)/2124*100; *enter validation set total sample here tho instead since roc dataset created off score set;
	precision=_POS_/(_POS_+_FALPOS_); 
	acc=_POS_+_NEG_; 
	lift=precision/0.34467; *enter pop prop of 1's here;
run;

/* Create visualization and remove depth observations at such rare occurrences where it is messing up graph */
proc sgplot data=roc;  
	where depth > 0.20;
	series y=lift x=depth; 
	refline 1.0 / axis=y; 
	title1 "Lift Chart for Validation Data"; 
	xaxis label="Depth (%)";
	yaxis label="Lift";
run; 
quit;

/* based on this looks like around 20-25% depth is around where the lift hits it's elbow point of sorts */
/* use to find quantiles of p_vals to create new cutoff point from lift chart at top 25% of p-vals */
proc univariate data=score;
	var p_1;
run;
/* The 3rd quartile was = to a p_1 val of 0.5289499 */

data lift_cutoff;
	set score;
	actual = f_ins;
	if p_1 > 0.5289499 then predicted = 1;
	else predicted = 0;
run;

/* Confusion matrix for lift cutoff */
proc freq data = lift_cutoff;
	tables actual*predicted;
run;
/* Current Model Stats on valid set with a .52849499 cutoff */
/* accuracy = 72.74% */
/* Sensitivity/Recall(TPR) = 46.77% */
/* 1 - specificity(FPR) = 13.31% */
/* Precision (PPV) = 65.35% */

/* Since no real estimates available for false negs/pos's going to just show the lift suggested one and the Youden/F1 */
/* to demonstrate potential differences in quality of prediction vs quantity that might matter depending on marekting costs */
